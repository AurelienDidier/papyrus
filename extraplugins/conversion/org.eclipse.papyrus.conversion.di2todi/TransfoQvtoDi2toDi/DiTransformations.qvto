
--Author Manel Fredj-CEA LIST


modeltype di uses 'http://www.eclipse.org/papyrus/0.7.0/sashdi';
modeltype di2 uses 'http://www.papyrusuml.org/di2';
modeltype notation uses 'http://www.eclipse.org/gmf/runtime/1.0.2/notation';
modeltype ecore uses 'http://www.eclipse.org/emf/2002/Ecore';


transformation Transfo(in src : di2, out target1 : notation, out target2:di);


mapping di2::Diagram::diagram2SashWindowsMngr(): di::SashWindowsMngr 
{
	--pageRefsclassandOp
	var pageRefs: Set(di::PageRef) := setPageRef();
	--pageList
	pageList := setPageList(pageRefs);

	--sashModel
	sashModel := setSashModel();
}

mapping notation::Diagram::diagram2PageRef(): di::PageRef
{
	emfPageIdentifier := self.oclAsType(ecore::EObject);
}  


-------------------------------------------------------------------------------------------------------------------------
----------------------------------------            Helpers for di                ---------------------------------------
-------------------------------------------------------------------------------------------------------------------------

helper setPageList(in pagerefs:Set(di::PageRef)): di::PageList
{
	var pageList := object di::PageList {
		availablePage := pagerefs;
	};
	
 	return pageList;
};
		
	
helper setPageRef(): Set(di::PageRef)
{
	var setPages : Set(di::PageRef) := Set{};
	var page: di::PageRef;
	target1.rootObjects()->forEach(diagram|diagram.oclIsTypeOf(notation::Diagram)) {
		--avoid empty pages for unsupported diagrams (currently deployment diagram)
		if (diagram.oclAsType(notation::Diagram).type != null) then {
			page := diagram.oclAsType(notation::Diagram).map diagram2PageRef();
			setPages += page;
		} endif;
	};
 	return setPages;
};	

	
helper setSashModel(): di::SashModel
{
	var sm := object di::SashModel{
		currentSelection:= object di::TabFolder{} ;
		windows := setWindows();
	};
	return sm;
};	

helper setWindows(): Set(di::Window)
{
	var setwindows : Set(di::Window);
	setwindows += setWindow();	
	return setwindows;
};	
	
helper setWindow() : di::Window {

	var child := object di::TabFolder {
	
		target1.rootObjects()->forEach(diagram|diagram.oclIsTypeOf(notation::Diagram)) {
			--avoid empty pages for unsupported diagrams (currently deployment diagram)
			if (diagram.oclAsType(notation::Diagram).type != null) then {
				var page : di::PageRef = object di::PageRef {
					emfPageIdentifier:= diagram.oclAsType(ecore::EObject)
				};
				children += page;
			} endif;
		};
	};
	
	var window := object di::Window {
		children := Set { child };
	};			
	return window;
};	
	
--helper SetChildren(): Set(di::PageRef){

	--var setchildren:Set(di::PageRef):=Set{};
	--var page: di::PageRef;

 	--return setchildren;
--};	
